
apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

dependencies {
	if(projectDir.getParentFile().equals(rootDir)) {
        implementation project(':core')
        testImplementation project(path: ':core', configuration: 'demo')
   	 } else {
        implementation project(':'+projectDir.getParentFile().name+':core')
   	 	testImplementation project(path: ':'+projectDir.getParentFile().name+':core', configuration: 'demo')
   	 }
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"

    testImplementation "games.spooky.gdx:gdx-nativefilechooser-desktop:1.0.0"
}

apply plugin: 'maven-publish'

archivesBaseName = "$libName-${project.name}"

// Demo
task demo(type: Jar) {
    baseName = "$libName-demo"

    from files(sourceSets.main.output.classesDirs)
    from sourceSets.test.output
    from {configurations.compile.collect {zipTree(it)}}
    from {configurations.testCompile.collect {zipTree(it)}}

    manifest {
        attributes 'Main-Class': "net.spookygames.gdx.sfx.demo.GdxSfxDemoDesktop"
    }
}

// Javadoc
task javadocJar (type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// Sources
task sourcesJar (type : Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId "$libName-${project.name}"
            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

apply from: '../license.gradle'
license.includes(["**/*.java"])
